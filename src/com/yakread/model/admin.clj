(ns com.yakread.model.admin
  (:require
   [com.biffweb.graph :as biff.graph :refer [defresolver]])
  (:import
   [java.time ZoneId]))

(defresolver recent-users
  {:output [{:admin/recent-users [:user/id]}]}
  [{:biff/keys [query now]} _]
  {:admin/recent-users
   (query {:select :user/id
           :from :user
           :where [:<= (.minusSeconds now (* 60 60 24 7)) :user/joined-at]})})

(defresolver dau
  {:output [:admin/dau]}
  [{:biff/keys [query now]} _]
  {:admin/dau
   (->> (query {:select :user-item/viewed-at
                :from :user-item
                :where [:<= (.minusSeconds now (* 60 60 24 30)) :user-item/viewed-at]})
        (mapv (fn [{:keys [user-item/viewed-at]}]
                (.. viewed-at
                    (atZone (ZoneId/of "America/Denver"))
                    (toLocalDate))))
        frequencies)})

(defresolver revenue
  {:output [:admin/revenue]}
  [{:biff/keys [query now]} _]
  {:admin/revenue
   (->> (query {:select [:ad-click/created-at :ad-click/cost]
                :from :ad-click
                :where [:<= (.minusSeconds now (* 60 60 24 30)) :ad-click/created-at]})
        (reduce (fn [acc {:ad-click/keys [created-at cost]}]
                  (let [date (.. created-at
                                 (atZone (ZoneId/of "America/Denver"))
                                 (toLocalDate))]
                    (update acc date (fnil + 0) cost)))
                {}))})

(defresolver ads
  {:output [{:admin/ads [:ad/id]}]}
  [{:biff/keys [query]} _]
  {:admin/ads (query {:select :ad/id :from :ad})})

(def module
  {:biff.graph/resolvers [recent-users
                          dau
                          revenue
                          ads]})
